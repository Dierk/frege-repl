module frege.repl.FregeRepl where

import frege.Version
import Java.Net
import Data.List
import Interpreter.FregeInterpreter
import Compiler.Data (Symbol)
import Compiler.Utilities as U()
import Compiler.EclipseUtil as EU()
import Java.Swing (JFrame, invokeLater)
import Java.Awt (Component, ActionListener)
import Repl.Gui
import frege.Prelude hiding (print)
import frege.Prelude (print pPrint)

data ReplEnv = ReplEnv {
  lastJavaSource :: Maybe String,
  lastExternalScript :: Maybe String,
  prompt :: String,
  interpreterEnv :: InterpreterEnv
} where
  default = ReplEnv {
    lastJavaSource = Nothing,
    lastExternalScript = Nothing,
    prompt = "frege> ",
    interpreterEnv = InterpreterEnv.default
  }

repl :: ConsoleReader -> ReplEnv -> IO ()
repl console env = do
  cmd <- read console env
  let res = eval cmd env
  print console cmd res
  loop console cmd res env

read :: ConsoleReader -> ReplEnv -> IO Command
read console state = do
   console.println ""
   line <- console.readLine state.prompt
   let readAgain = read console state
   case Command.parse line of
     Left err -> (console.println err) >> readAgain
     Right DefMulti -> readMultiline console >>= (return . Eval)
     Right Reload -> maybe readAgain (readExternal console state) state.lastExternalScript
     Right (Load path) -> readExternal console state path
     Right cmd -> return cmd

readExternal :: ConsoleReader -> ReplEnv -> String -> IO Command
readExternal console state (urlSpec@#.*?://.*#) = do
  url <- URL.new urlSpec
  let fetch = urlContents url >>= (return . Right)
      handleIOE (ioe :: IOException) = return $ Left ioe.getMessage
  script <- fetch `catch` handleIOE
  either (\err -> (console.println err) >> read console state)
    (return . LoadExternal urlSpec) script
readExternal console state filePath = do
   script <- safeSlurp filePath
   either (\err -> (console.println err) >> read console state)
                   (return . LoadExternal filePath) script

print :: ConsoleReader -> Command -> InterpreterResult -> IO ()
print console Java
  (InterpreterResult{typ=EvalSuccess, messages=(Message _ javaSrc:_)}) = javaSourceGUI javaSrc
print console (HelpDoc src)
  (InterpreterResult{typ=EvalSuccess, messages=(Message _ help:_)})    = helpGUI src help
print console _ (InterpreterResult{typ=EvalError,messages=errs})       = printMessages console errs
print console _ (InterpreterResult{typ=Def syms g, messages=msgs})     =
  do
    let symLabels = map (showSymbol g) $ filter (not . Symbol.{alias?}) syms
    when (not $ null symLabels) $ console.println $ intercalate newLine symLabels
    printMessages console msgs
print console _ (InterpreterResult{typ=ModuleDef moduleName _,messages=msgs}) = do
  console.println $ "Module: " ++ moduleName
  printMessages console msgs
print console _ (InterpreterResult{typ=EvalSuccess, messages=msgs})           = printMessages console msgs
print console _
  (InterpreterResult{typ=Interpret symbol g _, env=state})                    = do
      let res = showableSymbol state g symbol
          getValue (sym, g, newInterpEnv) = do
            valMaybe <- fieldValue (symbolClass sym g) (symbolVar sym) newInterpEnv.loader
            console.println $ maybe "undefined" id valMaybe
      maybe (console.println $ getSymbolType g symbol) getValue res
  `catch` (println . showThrowableCause)
print _ _ _                                                    = return ()

loop :: ConsoleReader -> Command -> InterpreterResult -> ReplEnv -> IO ()
loop console Quit _ _ = return ()
loop console Reset _ _ = repl console ReplEnv.default
loop console (Eval _) (InterpreterResult res newInterpEnv _) state =
  repl console (updateReplEnv res newInterpEnv state)
loop console (LoadExternal src script) (InterpreterResult res newInterpEnv _) state =
  repl console (updateReplEnv res newInterpEnv state.{lastExternalScript=Just src})
loop console _ (InterpreterResult _ newState _) state = repl console state

eval :: Command -> ReplEnv -> InterpreterResult
eval (Eval line) state = interpret line state.interpreterEnv
eval (Type expr) state = typeof expr state.interpreterEnv
eval (LoadExternal src script) state = interpret script state.interpreterEnv
eval (BrowseModule moduleName) state = browseModule moduleName state.interpreterEnv
eval Browse state = browse state.interpreterEnv
eval (HelpDoc name) state = docHelp name state.interpreterEnv
eval Java state = javaSource state
eval History state = history state.interpreterEnv
eval Reset _ = InterpreterResult EvalSuccess InterpreterEnv.default []
eval Version state = InterpreterResult EvalSuccess state.interpreterEnv [Message INFO version]
eval Help state = InterpreterResult EvalSuccess state.interpreterEnv [Message INFO help]
eval _ state = InterpreterResult EvalSuccess state.interpreterEnv []

javaSource :: ReplEnv -> InterpreterResult
javaSource state = maybe genJavaSource cachedJavaSource state.lastJavaSource where
  cachedJavaSource m = InterpreterResult EvalSuccess state.interpreterEnv [Message INFO m]
  genJavaSource = either fail success $ javasrc currDefs state.interpreterEnv.{predefs=[]} where
    fail msgs = InterpreterResult EvalError state.interpreterEnv msgs
    success jsrc = InterpreterResult EvalSuccess state.interpreterEnv [Message INFO jsrc]
  currDefs = concat . intersperse newLine $ reverse state.interpreterEnv.predefs

updateReplEnv :: InterpreterResultType -> InterpreterEnv -> ReplEnv -> ReplEnv
updateReplEnv (ModuleDef _ javaSource) newInterpEnv state =
  -- Update Class Loader with the module class file and the generated Java source
  state.{lastJavaSource=Just javaSource, interpreterEnv<-InterpreterEnv.{loader=newInterpEnv.loader}}

-- Update with generated java source
updateReplEnv (Interpret _ _ javaSource) newInterpEnv state = state.{lastJavaSource=Just javaSource}

-- Update with the new definitions
updateReplEnv (Def _ _) newInterpEnv state =
  state.{lastJavaSource=Nothing, interpreterEnv<-InterpreterEnv.{predefs=newInterpEnv.predefs}}

-- In all other cases, do not modify state
updateReplEnv _ _ state = state

history :: InterpreterEnv -> InterpreterResult
history state = InterpreterResult EvalSuccess state [Message INFO . unlines . reverse $ state.predefs]

printMessages :: ConsoleReader -> [Message] -> IO ()
printMessages console [] = return ()
printMessages console msgs = console.println $ Message.showMessages msgs

readMultiline :: ConsoleReader -> IO String
readMultiline console = intercalate newLine . reverse <$> f [] where
  f acc = do
    line <- console.readLine ""
    if (line == ":}")
      then console.println "" >> return acc
      else f (line : acc)


cmdHelp = [(":type <expression>", "Display the type of an expression."),
            (":browse <module name>", "Display the names in a module if " ++
                "a module name is provided otherwise display the names in the default REPL module."),
            (":load <url or file>", "Load Frege code snippets from an URL or file."),
            (":java", "View Java translation of last compiled Frege source."),
            (":r", "Reload the last script file."),
            (":history", "Display the source history for definitions in the default REPL module."),
            (":reset", "Reset the session discarding all evaluated scripts."),
            (":version", "Display Frege version."),
            (":{", "Start multiline definitions."),
            (":}", "End multiline defintions."),
            (":help <name>", "Display the documentation for the given name." ++
                            " If the name is not provided, display this help message."),
            (":q or :quit", "Quit REPL")]

f `on` g = \x \y -> f (g x) (g y)

renderCmdHelp cmdHelp = map render cmdHelp where
    maxLeftIndent = length . fst $ maximumBy (compare `on` (length . fst)) cmdHelp
    indent cmd desc width = cmd ++ (concat $ replicate (width - cmd.length) " ") ++
        " - " ++ desc
    render (cmd, desc) = indent cmd desc maxLeftIndent

help = intercalate newLine $ header ++ body where
    header = ["At the prompt, you can enter Frege code snippets to get them evaluated.",
            "The output or compilation errors will be printed below the prompt.",
            "In addition to Frege code, the following commands are supported:\n"]
    body = renderCmdHelp cmdHelp


safeSlurp :: String -> IO (String | String)
safeSlurp file = fetch file `catch` handleIOE where
        fetch file = fileContents file >>= (return . Right)
        handleIOE :: IOException -> IO (Either String String)
        handleIOE ioe = return $ Left ioe.getMessage

data ConsoleReader = mutable native jline.console.ConsoleReader where
  native new :: () -> IO ConsoleReader throws IOException
  native readLine :: ConsoleReader -> String -> IO String throws IOException
  native readLine' readLine :: ConsoleReader -> IO String throws IOException
  native println :: ConsoleReader -> String -> IO () throws IOException
  native setPrompt :: ConsoleReader -> String -> IO ()
  native setExpandEvents :: ConsoleReader -> Bool -> IO ()

urlContents url = do
    inStream <- URL.openStream url
    scanner <- Scanner.new inStream
    scanner.useDelimiter "\\Z"
    scanner.next `finally` scanner.close

fileContents filePath = do
    file <- File.new filePath
    scanner <- Scanner.fromFile file "utf-8"
    scanner.useDelimiter "\\Z"
    scanner.next `finally` scanner.close

data Scanner = mutable native java.util.Scanner where
    native new :: InputStream -> IO Scanner
    native fromFile new :: MutableIO File -> String -> IO Scanner throws FileNotFoundException
    native useDelimiter :: Scanner -> String -> IO ()
    native next :: Scanner -> IO String

javaSourceGUI :: String -> IO ()
javaSourceGUI javaSource = do
   frame::JFrame <- JFrame.new "Java Source"
   newContentPane::JPanel <- BorderLayout.new () >>= JPanel.new
   frame.setDefaultCloseOperation JFrame.dispose_on_close
   editor::JEditorPane <- JEditorPane.new "text/plain" javaSource
   editor.setEditable false
   scrollPane <- JScrollPane.new editor
   dimension <- Dimension.new 600 600
   JComponent.setPreferredSize scrollPane dimension
   Container.add newContentPane scrollPane (asObject BorderLayout.center)
   frame.setContentPane newContentPane
   frame.pack
   frame.setVisible true


helpGUI :: String -> String -> IO ()
helpGUI title content = do
    frame <- JFrame.new (title ++ " - Documentation")
    frame.setDefaultCloseOperation JFrame.dispose_on_close
    newContentPane:: JPanel <- BorderLayout.new () >>= JPanel.new
    let html :: String
        html = substring content $ indexOf content "<HTML>" -- Skip DOCTYPE
    editor::JEditorPane <- JEditorPane.new "text/html" html
    editor.setEditable false
    helpLinksListener <- FregeJavaProxy.with showDesktop HyperlinkListener.clazz
    editor.addHyperlinkListener helpLinksListener
    scrollPane <- JScrollPane.new editor
    dimension <- Dimension.new 600 600
    JComponent.setPreferredSize scrollPane dimension
    Container.add newContentPane scrollPane (asObject BorderLayout.center)
    newContentPane.setOpaque true
    frame.setContentPane newContentPane
    frame.pack
    frame.setVisible true

data FregeJavaProxy = pure native frege.interpreter.javasupport.FregeJavaProxy where
  native with frege.interpreter.javasupport.FregeJavaProxy.with ::
    (Object -> Method -> ObjectArr -> ST s a) -> Class c -> IO c

showDesktop :: Object -> Method -> ObjectArr -> IO ()
showDesktop _ _ args = do
  event <- return $ asHyperlinkEvent $ args.elemAt 0
  d <- Desktop.getDesktop ()
  desktopSupported <- d.isDesktopSupported
  let url = HyperlinkEvent.getURL event
      navigateRelative = either throw d.browse . URI.new $ fixHelpLink event.getDescription
      navigate = maybe navigateRelative (\u -> toURI u >>= d.browse) url
  when (event.getEventType == HyperlinkEvent_EventType.activated) navigate


fixHelpLink s | Just (Just relative : _)
                  <- s `match` #^\.\.(.*)$# = "http://try.frege-lang.org/doc/frege" ++ relative
fixHelpLink s = s

native toURI :: URL -> ST s URI throws URISyntaxException
pure native asObject "(java.lang.Object)" :: a -> Object


pure native isEmpty :: String -> Bool
pure native replaceAll :: String -> String -> String -> String
pure native indexOf :: String -> String -> Int
pure native substring :: String -> Int -> String
                         | String -> Int -> Int -> String

data Command = Version | Help | HelpDoc String | Browse |
    BrowseModule String | Type String | Load String | LoadExternal String String | Eval String |
    Java | DefMulti | History | Reload | Reset | Skip | Quit where

    parse   :: String -> Either String Command
    parse s = parse' $ trim s where
        parse' "" = Right Skip
        parse' #^:# = parseCommand s
        parse' script = Right $ Eval script

    parseCommand s | Just (Just moduleName : _) <- s `match` #:browse\s+(.*)# = Right $ BrowseModule moduleName
    parseCommand s | Just (Just path : _) <- s `match` #^:l.*?\s+(.*)# = Right $ Load path
    parseCommand s | Just (Just src : _) <- s `match` #:help\s+(.*)# = Right $ HelpDoc src
    parseCommand s | Just (Just expr : _) <- s `match` #^:t.*?\s+(.*)# = Right $ Type expr
    parseCommand  ":version" = Right Version
    parseCommand ":browse" = Right $ Browse
    parseCommand ":{" = Right DefMulti
    parseCommand ":history" = Right History
    parseCommand ":java" = Right Java
    parseCommand ":r" = Right Reload
    parseCommand ":reset" = Right Reset
    parseCommand ":q" = Right Quit
    parseCommand ":quit" = Right Quit
    parseCommand ":help" = Right Help
    parseCommand otherwise = Left "Invalid Command"

derive Eq Command
derive Show Command

vmVendor = maybe "" id $ System.getProperty "java.vm.vendor"
vmName = maybe "" id $ System.getProperty "java.vm.name"
javaVersion = maybe "" id $ System.getProperty "java.version"

pure native toString :: Object -> String
pure native trim :: String -> String

welcome = "Welcome to Frege " ++ version ++ " (" ++ vmVendor ++ " " ++ vmName ++
    ", " ++ javaVersion ++ ")"

main _ = do
  console <- ConsoleReader.new ()
  console.setPrompt ReplEnv.default.prompt
  console.setExpandEvents false
  console.println welcome
  repl console ReplEnv.default
  System.exit 0
